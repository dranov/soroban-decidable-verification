#lang ivy1.8

module decidable_integer = {
    type this
    alias t = this

    individual minus_one : t
    individual zero : t
    individual one : t

    relation lt(X:t,Y:t)
    relation plus(X:t,Y:t,Z:t)
    relation minus(X:t,Y:t,Z:t)
    relation mult(X:t,Y:t,Z:t)
    relation divi(X:t,Y:t,Z:t)

    # Derived relations
    # FIXME: there's some breakage if we use the definition facility
    # rather, we have the *_def properties to achieve the same thing
    # this is probably a bug in Ivy
    relation lte(X:t, Y:t)# = lt(X, Y) | X = Y
    relation gt(X:t, Y:t)# = ~lt(X, Y) & X ~= Y  # i.e. ~lte(X, Y)
    relation gte(X:t, Y:t)# = ~lt(X, Y)
    relation succ(X:t, Y:t)# = plus(X, one, Y)

    # FIXME: we really want to fix the definition facility for sqrt
    # y = sqrt(x) <-> y^2 <= x < (y+1)^2
    # relation isqrt(X:t, Y:t)# = 
        # gt(X, zero) & gt(Y, zero)
        # & exists YY. mult(Y, Y, YY) & lte(YY, X)
        # & exists Y1, YY1. succ(Y, Y1) & mult(Y1, Y1, YY1) & lt(X, YY1)

    action next(x:t) returns (y:t)
    action prev(x:t) returns (y:t)

    # function (X:t + Y:t) : t
    # function (X:t - Y:t) : t
    # A function is a relation with [functionality] and [totality]
    # axioms. We state the former and instantiate the latter on demand.
    action add(x:t,y:t) returns (z:t)
    action sub(x:t,y:t) returns (z:t)
    action mul(x:t,y:t) returns (z:t)
    action div(x:t,y:t) returns (z:t)

    action sqrt(x:t) returns (y:t)

    action min(x:t,y:t) returns (z:t)
    action max(x:t,y:t) returns (z:t)

    action opposite_lemma(x:t) # -x exists and x + (-x) = 0
    action add_lemma(x:t,y:t)  # x + y exists
    action sub_lemma(x:t,y:t)  # x - y exists

    object spec = {
        property [constants_order] succ(minus_one, zero) & succ(zero, one)
        property [minus_one_zero] 
            lt(minus_one, zero) & ~exists X. lt(minus_one, X) & lt(X, zero)
        property [zero_one] 
            lt(zero, one) & ~exists X. lt(zero, X) & lt(X, one)

        # Derived relations
        property [lte_def] lte(X, Y) <-> lt(X, Y) | X = Y
        property [gt_def] gt(X, Y) <-> ~lt(X, Y) & X ~= Y
        property [gte_def] gte(X, Y) <-> ~lt(X, Y)
        property [succ_def] succ(X, Y) <-> plus(X, one, Y)
        property [succ_not_opposite]
            succ(X, Y) -> ~(plus(X, Y, zero) & minus(zero, X, Y))
        # FIXME: error: Solver produced inconclusive result
        # property [isqrt_def]
        #     isqrt(X, Y) <-> (
        #         gte(X, zero) & gte(Y, zero)
        #         & exists YY. mult(Y, Y, YY) & lte(YY, X)
        #         & exists Y1, YY1. succ(Y, Y1) & mult(Y1, Y1, YY1) & lt(X, YY1)
        #     )

        # Properties of order
        property [transitivity] lt(T,U) & lt(U,V) -> lt(T,V)
        property [antisymmetry] ~(lt(T,U) & lt(U,T))
        property [totality] lt(T,U) | T = U | lt(U,T)
        property [irreflexivity] ~lt(T,T) # implied by the above

        # Functionality (without totality)
        property [plus_functionality]
            plus(X, Y, Z1) & plus(X, Y, Z2) -> Z1 = Z2
        property [minus_functionality]
            minus(X, Y, Z1) & minus(X, Y, Z2) -> Z1 = Z2

        # Properties of addition
        property [plus_commutative]
            plus(X, Y, Z) -> plus(Y, X, Z)
        property [plus_associative]
            plus(A, B, AB) & plus (AB, C, ABC1) &
            plus(B, C, BC) & plus(A, BC, ABC2) -> ABC1 = ABC2
        property [plus_zero]
            plus(zero, X, X)
        property [plus_positive]
            plus(X, Y, Z) & gt(Y, zero) -> lt(X, Z)

        # Properties of subtraction
        property [minus_zero]
            minus(X, zero, X)
        property [minus_self]
            minus(X, X, zero)
        property [minus_plus]
            minus(X, Y, Z) <-> plus(Z, Y, X)
        property [minus_lt]
            minus(X, Y, Z) & lt(X, Y) -> lt(Z, zero)
        property [minus_gte] # a version of the above with ~lt(X, Y)
            minus(X, Y, Z) & gte(X, Y) -> gte(Z, zero)

        # Properties of multiplication
        property [mult_commutative]
            mult(X, Y, Z) -> mult(Y, X, Z)
        property [mult_associative]
            mult(A, B, AB) & mult (AB, C, ABC1) &
            mult(B, C, BC) & mult(A, BC, ABC2) -> ABC1 = ABC2
        property [mult_zero]
            mult(zero, X, zero)
        property [mult_one]
            mult(one, X, X)
        property [mult_pos_pos]
            mult(X, Y, Z) & gt(X, zero) & gt(Y, zero) -> gt(Z, zero)
        property [mult_pos_neg]
            mult(X, Y, Z) & gt(X, zero) & lt(Y, zero) -> lt(Z, zero)
        property [mult_neg_neg]
            mult(X, Y, Z) & lt(X, zero) & lt(Y, zero) -> gt(Z, zero)

        # Properties of division
        property [div_one]
            divi(X, one, X)
        property [div_self]
            X ~= zero -> divi(X, X, one)
        
        # These properties cause decidability issues
        # property [every_number_has_negative]
        #     exists Y. plus(X, Y, zero) & minus(zero, Y, X)

        #################
        # Derived facts #
        #################
        # The properties are sufficient to establish:
        # conjecture [succ_distinct] succ(X, Y) -> X ~= Y
        # conjecture [constants_distinct] ~(zero = one) & ~(minus_one = zero) & ~(minus_one = one)
        # conjecture [minus_one_cancel]
        #     plus(minus_one, one, zero) & minus(zero, minus_one, one) & minus(zero, one, minus_one)
        # conjecture [succ_eq] succ(X, Z) & succ(Y, Z) -> X = Y
        # conjecture [succ_no_gaps]
        #     succ(X, Y) -> ~exists Z.  lt(X, Z) & lt(Z, Y)
        # conjecture [no_gaps_succ]
        #     lt(X, Y) & (~exists Z.  lt(X, Z) & lt(Z, Y)) -> succ(X, Y)

        ##########################
        # Action implementations #
        ##########################
        
        implement opposite_lemma(x:t) {
            assume exists Z. plus(x, Z, zero) & minus(zero, Z, x);
        }
        implement add_lemma(x:t, y:t) {
            assume exists Z. plus(x, y, Z);
        }
        implement sub_lemma(x:t, y:t) {
            assume exists Z. minus(x, y, Z);
        }

        after add {
            # call add_lemma(x, y); call sub_lemma(z, x); call sub_lemma(z, y);
            # call opposite_lemma(x); call opposite_lemma(y); call opposite_lemma(z);
            assume plus(x, y, z);
        }
        after sub {
            # call sub_lemma(x, y); call add_lemma(z, y);
            # call opposite_lemma(x); call opposite_lemma(y); call opposite_lemma(z);
            assume minus(x, y, z);
        }
        after mul {
            assume mult(x, y, z);
        }
        after div {
            assume divi(x, y, z);
        }
        after sqrt {
            assume gte(x, zero) & gte(y, zero)
                & exists YY. mult(y, y, YY) & lte(YY, x)
                & exists Y1, YY1. succ(y, Y1) & mult(Y1, Y1, YY1) & lt(x, YY1)
        }
        after min {
            if lt(x, y) {
                assume z = x;
            } else {
                assume z = y;
            }
        }
        after max {
            if gt(x, y) {
                assume z = x;
            } else {
                assume z = y;
            }
        }
        after next {
            assert plus(x, one, y);
            assert minus(y, one, x);
        }
        after prev {
            assert minus(x, one, y);
            assert plus(y, one, x);
        }
    }

    object impl = {
        interpret t -> int
        definition minus_one = 0 - 1
        definition zero = 0
        definition one = 1
        definition lt(X, Y) = (X < Y)
        definition plus(X, Y, Z) = (Z = X + Y)
        definition minus(X, Y, Z) = (Z = X - Y)
        definition mult(X, Y, Z) = (Z = X * Y)
        definition divi(X, Y, Z) = (Z = X / Y)

        # FIXME: we cannot verify this
        # error: Solver produced inconclusive result
        # y = sqrt(x) <-> y^2 <= x < (y+1)^2
        # definition isqrt(X, Y) = 
        #     (X >= 0) & (Y >= 0)
        #     & (Y * Y <= X) & (X < (Y + 1) * (Y + 1))
        
        # Derived relations
        # FIXME: see the problem above with derived relations
        definition lte(X, Y) = (X <= Y)
        definition gt(X, Y) = (X > Y)
        definition gte(X, Y) = (X >= Y)
        definition succ(X, Y) = (Y = X + 1)
    }
    isolate iso = impl,spec
}

# Wrapper over `int` that exposes the same interface as `integer`
module interpreted_integer = {
    type this
    alias t = this
    interpret t -> int
    
    individual minus_one : t
    individual zero : t
    individual one : t

    relation lt(X:t,Y:t) = X < Y
    relation plus(X:t,Y:t,Z:t) = Z = X + Y
    relation minus(X:t,Y:t,Z:t) = Z = X - Y
    relation mult(X:t,Y:t,Z:t) = (Z = X * Y)
    relation divi(X:t,Y:t,Z:t) = (Z = X / Y)
    relation lte(X:t, Y:t) = X <= Y
    relation gt(X:t, Y:t) = X > Y
    relation gte(X:t, Y:t) = X >= Y
    relation succ(X:t, Y:t) = Y = X + 1
    

    action next(x:t) returns (y:t)
    action prev(x:t) returns (y:t)

    action add(x:t,y:t) returns (z:t)
    action sub(x:t,y:t) returns (z:t)
    action mul(x:t,y:t) returns (z:t)
    action div(x:t,y:t) returns (z:t)

    action sqrt(x:t) returns (y:t)

    action min(x:t,y:t) returns (z:t)
    action max(x:t,y:t) returns (z:t)

    object spec = {
        property [minus_one_def] minus_one = 0 - 1
        property [zero_def] zero = 0
        property [one_def] one = 1

        after next {
            assume y = x + 1
        }
        after prev {
            assume y = x - 1
        }
        after add {
            assume z = x + y
        }
        after sub {
            assume z = x - y
        }
        after mul {
            assume z = x * y
        }
        after div {
            assume z = x / y
        }
        after sqrt {
            assume (x >= 0) & (y >= 0)
            & (y * y <= x) & (x < (y + 1) * (y + 1))
        }
        after min {
            if x < y {
                assume z = x;
            } else {
                assume z = y;
            }
        }
        after max {
            if x > y {
                assume z = x;
            } else {
                assume z = y;
            }
        }
    }

    object impl = {
        interpret t -> int
        definition minus_one = 0 - 1
        definition zero = 0
        definition one = 1
    }

    isolate iso = impl,spec
}

